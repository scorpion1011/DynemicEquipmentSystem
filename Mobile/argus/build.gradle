buildscript {
    repositories {
        jcenter()
    }
    
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'checkstyle'

preBuild.dependsOn 'checkstyle'
check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle) {
    configFile file("${project.projectDir}/config/quality/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()

    // set to false if you want to consider warning as error
    ignoreFailures = true
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 5
        versionName "0.2.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // setting custom name for output
        project.ext.set("archivesBaseName", "Argus-android-" + defaultConfig.versionName
                + "-vc" + defaultConfig.versionCode);
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet true
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        ignoreWarnings false
        // if true, emit full/absolute paths to files with errors (true by default)
        //absolutePaths true
        // if true, check all issues, including those that are off by default
        checkAllWarnings true
        // if true, treat all warnings as errors
        warningsAsErrors false
        // turn off checking the given issue id's
        disable 'TypographyFractions', 'TypographyQuotes'
        // turn on the given issue id's
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        // check *only* the given issue id's
        //check 'NewApi', 'InlinedApi'
        // if true, don't include source code lines in the error output
        noLines false
        // if true, show all locations for an error, do not truncate lists, etc.
        showAll true
        // Fallback lint configuration (default severities, etc.)
        lintConfig file("default-lint.xml")
        // if true, generate a text report of issues (false by default)
        textReport false
        // location to write the output; can be a file or 'stdout'
        textOutput 'stdout'
        // if true, generate an XML report for use by for example Jenkins
        xmlReport false
        // file to write report to (if not specified, defaults to lint-results.xml)
        xmlOutput file("build/reports/lint/lint-report.xml")
        // if true, generate an HTML report (with issue explanations, sourcecode, etc)
        htmlReport true
        // optional path to report (default will be lint-results.html in the builddir)
        htmlOutput file("build/reports/lint/lint-report.html")
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.google.android.gms:play-services-auth:11.0.4'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.facebook.android:facebook-android-sdk:4.22.0'
    compile 'com.google.code.gson:gson:2.7'
    testCompile 'junit:junit:4.12'
    provided 'org.projectlombok:lombok:1.16.16'
    provided 'org.glassfish:javax.annotation:10.0-b28'
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'argus'

    publishedGroupId = 'com.moldedbits.argus'
    libraryName = 'argus'
    artifact = 'argus'

    libraryDescription = 'Argus is a library to make onboarding simple in Android apps'

    siteUrl = 'https://github.com/moldedbits/argus-android'
    gitUrl = 'https://github.com/moldedbits/argus-android.git'

    libraryVersion = '0.2.0'

    developerId = 'moldedbits'
    developerName = 'Moldedbits'
    developerEmail = 'hello@moldedbits.com'

    licenseName = 'Apache 2.0'
    licenseUrl = 'https://opensource.org/licenses/Apache-2.0'
    allLicenses = ["Apache-2.0"]
}

apply from: 'install.gradle'
apply from: 'bintray.gradle'